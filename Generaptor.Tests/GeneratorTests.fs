// SPDX-FileCopyrightText: 2024-2025 Friedrich von Never <friedrich@fornever.me>
//
// SPDX-License-Identifier: MIT

module Generaptor.Tests.GeneratorTests

open Xunit

open Generaptor
open Generaptor.GitHubActions
open type Generaptor.GitHubActions.Commands

let private doTest (expected: string) wf =
    let actual = Serializers.Stringify wf Map.empty TestFramework.MockActionsClient
    Assert.Equal(expected.ReplaceLineEndings "\n", actual)

let private doTestOnExistingWorkflow (existing: string) (expected: string) wf =
    let existingVersions = Serializers.ExtractVersions existing
    let actual = Serializers.Stringify wf existingVersions TestFramework.MockActionsClient
    Assert.Equal(expected.ReplaceLineEndings "\n", actual)

[<Fact>]
let ``Basic workflow gets generated``(): unit =
    let wf = workflow("wf") [|
        name "Main"
        onPushTo "main"

        job "main" [|
            needs "another"
            runsOn "ubuntu-latest"
            step(uses = "actions/checkout@v4")
        |]
    |]
    let expected = """name: Main
on:
  push:
    branches:
    - main
jobs:
  main:
    needs:
    - another
    runs-on: ubuntu-latest
    steps:
    - uses: actions/checkout@v4
"""
    doTest expected wf

[<Fact>]
let ``Condition tests``(): unit =
    let wf = workflow("wf") [|
        onPushTo "main"
        job "main" [|
            step(uses = "aaa")
            step(uses = "bbb", condition = "goes here")
        |]
    |]
    let expected = """on:
  push:
    branches:
    - main
jobs:
  main:
    steps:
    - uses: aaa
    - if: goes here
      uses: bbb
"""
    doTest expected wf

[<Fact>]
let ``Environment tests``(): unit =
    let wf = workflow("wf") [|
        onPushTo "main"
        job "main" [|
            step(uses = "aaa")
            step(uses = "bbb", env = Map.ofList [ "foo", "bar" ])
        |]
    |]
    let expected = """on:
  push:
    branches:
    - main
jobs:
  main:
    steps:
    - uses: aaa
    - uses: bbb
      env:
        foo: bar
"""
    doTest expected wf

[<Fact>]
let ``Strategy test``(): unit =
    let wf = workflow "wf" [
        job "main" [|
            strategy(failFast = false, matrix = [|
                "image", [
                    "macos-latest"
                    "ubuntu-latest"
                    "windows-latest"
                ]
            |])
        |]
    ]
    let expected = """on: {}
jobs:
  main:
    strategy:
      matrix:
        image:
        - macos-latest
        - ubuntu-latest
        - windows-latest
      fail-fast: false
"""
    doTest expected wf

[<Fact>]
let ``Complex strategy test``(): unit =
    let wf = workflow "wf" [
        job "main" [|
            strategy(matrix = [|
                "config", [
                    Map.ofList [
                        "name", "macos"
                        "image", "macos-latest"
                    ]
                    Map.ofList [
                        "name", "linux"
                        "image", "ubuntu-24.04"
                    ]
                    Map.ofList [
                        "name", "windows"
                        "image", "windows-2022"
                    ]
                ]
            |])
        |]
    ]
    let expected = """on: {}
jobs:
  main:
    strategy:
      matrix:
        config:
        - image: macos-latest
          name: macos
        - image: ubuntu-24.04
          name: linux
        - image: windows-2022
          name: windows
"""
    doTest expected wf

[<Fact>]
let ``Permissions block``(): unit =
    let wf = workflow "wf" [
        workflowPermission(PermissionKind.Actions, AccessKind.Read)
        workflowPermission(PermissionKind.Pages, AccessKind.Write)
        workflowPermission(PermissionKind.IdToken, AccessKind.Write)
    ]
    let expected = """on: {}
permissions:
  actions: read
  id-token: write
  pages: write
jobs: {}
"""
    doTest expected wf

[<Fact>]
let ``Concurrency block``(): unit =
    let wf = workflow "wf" [
        concurrency(group = "pages", cancelInProgress = false)
    ]
    let expected = """on: {}
concurrency:
  group: pages
  cancel-in-progress: false
jobs: {}
"""
    doTest expected wf

[<Fact>]
let ``Autogenerated version fetching test``(): unit =
    let wf = workflow "wf" [
        job "main" [|
            step(usesSpec = Auto "ForNeVeR/ChangelogAutomation.action")
        |]
    ]
    let expected = """on: {}
jobs:
  main:
    steps:
    - uses: ForNeVeR/ChangelogAutomation.action@v10
"""
    doTest expected wf

[<Fact>]
let ``Existing version fetching test``(): unit =
    let wf = workflow "wf" [
        job "main" [|
            step(usesSpec = Auto "ForNeVeR/ChangelogAutomation.action")
            step(usesSpec = Auto "ForNeVeR/ChangelogAutomation.action")
        |]
    ]
    let existing = """on: {}
jobs:
  main:
    steps:
    - uses: ForNeVeR/ChangelogAutomation.action@v1
"""
    let expected = """on: {}
jobs:
  main:
    steps:
    - uses: ForNeVeR/ChangelogAutomation.action@v1
    - uses: ForNeVeR/ChangelogAutomation.action@v1
"""
    doTestOnExistingWorkflow existing expected wf

[<Fact>]
let ``Extract several versions``(): unit =
    let existing = """on: {}
jobs:
  main:
    steps:
    - uses: ForNeVeR/ChangelogAutomation@v1
    - uses: ForNeVeR/ChangelogAutomation@v2
    - uses: ForNeVeR/ChangelogAutomation@v3
    - uses: ForNeVeR/ChangelogAutomation@nonsense
"""
    Assert.Equivalent(
        Map.ofArray [| "ForNeVeR/ChangelogAutomation", ActionVersion "v3" |],
        Serializers.ExtractVersions existing
    )

[<Fact>]
let ``Unparseable version ok with no alternative``(): unit =
    let existing = """on: {}
jobs:
  main:
    steps:
    - uses: ForNeVeR/ChangelogAutomation@nonsense
"""
    Assert.Equivalent(
        Map.ofArray [| "ForNeVeR/ChangelogAutomation", ActionVersion "nonsense" |],
        Serializers.ExtractVersions existing
    )

[<Fact>]
let ``Unparseable version error``(): unit =
    let existing = """on: {}
jobs:
  main:
    steps:
    - uses: ForNeVeR/ChangelogAutomation@nonsense1
    - uses: ForNeVeR/ChangelogAutomation@nonsense2
"""
    let ex = Assert.Throws(fun() -> Serializers.ExtractVersions existing |> ignore)
    Assert.Equal("Cannot determine any parseable version for action ForNeVeR/ChangelogAutomation.", ex.Message)
